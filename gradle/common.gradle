apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

dependencies {
  testImplementation libs.junit_jupiter
  testRuntimeOnly libs.junit_launcher

  if (project.name != "axelor-tomcat") {
    implementation libs.slf4j_logback
  }
}

configurations {
  // Remove Junit4, only keep Junit5
  testImplementation.exclude group: 'junit', module: 'junit'
  testImplementation.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'

  resolutionStrategy {
    force libs.slf4j_api
    force 'org.apache.shiro:shiro-web:1.13.0:jakarta'
    force 'org.apache.shiro:shiro-core:1.13.0:jakarta'
    force 'org.apache.shiro:shiro-guice:1.13.0:jakarta'
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs = ['-Xlint:-options']
     options.incremental = true
}
